// defaults to cdh5version from gradle.properties file, but can be overriden via -Pcdhversion=cdh4 on command line
// pulls all actual property values from gradle.properties
def cdh_version = project.hasProperty('cdhversion') && cdhversion == "cdh4" ? cdh4version : cdh5version

description = 'sample-project-mapreduce'

dependencies {
	compile project(':sample-project-core')
	compile 'org.zephyr:zephyr-mapreduce:0.2.0'
	hadoopProvided group: 'org.apache.hadoop', name: 'hadoop-client', version: cdh_version
	hadoopProvided 'org.slf4j:slf4j-api:1.6.6'
	testCompile 'junit:junit:4.11'
}

task distJars(dependsOn: 'distConf', type: Copy) {
	from configurations.runtime - configurations.hadoopProvided
	from jar
	include "**/*.jar"
	into "${buildDir}/dist/lib"
}

task distConf(dependsOn: 'assemble', type: Copy) {
	from "dist/conf"
	from project(":sample-project-core").fileTree("conf")
	into "${buildDir}/dist/conf"
}

task dist(dependsOn: 'distJars', type: Copy) {
	from "dist"
	include "*.sh"
	into "${buildDir}/dist"
}
